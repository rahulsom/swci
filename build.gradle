buildscript {
    dependencies {
        classpath 'com.h2database:h2:1.4.191'
    }
}

plugins {
    id 'nu.studer.jooq' version '2.0.4'
    id 'org.flywaydb.flyway' version '4.1.2'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

group 'com.github.rahulsom'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'application'

repositories {
    jcenter()
}

dependencies {
    // Groovy is awesome
    compile 'org.codehaus.groovy:groovy-all:2.4.10'

    // WDM manages browsers. Selenium talks to browsers. Geb makes selenium nicer.
    compile 'io.github.bonigarcia:webdrivermanager:1.6.1'
    compile 'org.gebish:geb-core:1.1.1'
    compile 'org.seleniumhq.selenium:selenium-chrome-driver:3.3.1'

    // Google Cloud Pubsub is how we get our messages
    compile 'com.google.cloud:google-cloud-pubsub:0.9.4-alpha'

    // H2 needs to be in multiple scopes for jooq and flyway to work
    compile 'com.h2database:h2:1.4.194'
    jooqRuntime 'com.h2database:h2:1.4.194'

    // JOOQ is a poor man's hibernate
    compile 'org.jooq:jooq'

    // Jackson is used for parsing messages from Google Pubsub
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.6'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.8.6'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

flyway {
    url = 'jdbc:h2:file:./swci'
    user = 'sa'
    password = 'secret'
}

jooq {
    version = '3.9.1'
    edition = 'OSS'

    swci(sourceSets.main) {
        jdbc {
            driver = 'org.h2.Driver'
            url = 'jdbc:h2:./swci'
            user = 'sa'
            password = 'secret'
        }

        generator {
            name = 'org.jooq.util.DefaultGenerator'
            database {
                name = 'org.jooq.util.h2.H2Database'
                includes = '.*'
                excludes = 'information_schema.*'
            }
            target {
                packageName = 'com.github.rahulsom.swci'
            }
        }
    }

}

mainClassName = 'com.github.rahulsom.swci.SwciApplication'

tasks.findByName('generateSwciJooqSchemaSource').dependsOn 'flywayMigrate'
tasks.findByName('compileGroovy').dependsOn 'generateSwciJooqSchemaSource'